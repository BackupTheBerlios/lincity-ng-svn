dnl Process this file with autoconf to produce a configure script.

dnl Initialization
AC_INIT(colour.pal)
AM_INIT_AUTOMAKE(lincity,1.12pre47)

AC_ARG_WITH(gzip,  [  --with-gzip             use gzip])
AC_ARG_WITH(svga,  [  --with-svga             use svga library])
AC_ARG_ENABLE(debug,[  --enable-debug          turn on debugging [default=no]],
if eval "test x$enable_debug = xyes"; then
  DEBUGFLAG="-g"
fi)

dnl Checks for programs.
AC_ISC_POSIX
AC_PROG_CC
AC_AIX
AC_PROG_MAKE_SET

dnl Autodetect presence of gzip
if eval "test x$with_gzip = x"; then
  AC_CHECK_PROG(with_gzip,gzip,"yes")
fi
if eval "test x$with_gzip = xyes"; then
  AC_DEFINE(HAVE_GZIP)
fi

dnl Checks for libraries.
AC_PATH_XTRA

dnl "$LIBS"          (blank)
dnl "$X_LIBS"        (-L/usr/X11R6/lib)
dnl "$X_PRE_LIBS"    (-lSM -lICE)
dnl "$X_EXTRA_LIBS"  (blank)
CFLAGS="$CFLAGS $X_CFLAGS"
LIBS="$LIBS $X_LIBS $X_PRE_LIBS $X_EXTRA_LIBS"

dnl Checks for header files.
AM_CONFIG_HEADER(config.h)
AC_STDC_HEADERS
AC_HEADER_DIRENT
AC_HEADER_TIME
AC_CHECK_HEADERS(string.h strings.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_EXEEXT
AC_CHECK_SIZEOF(char)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_PROG_RANLIB

dnl Checks for library functions.
AC_CHECK_FUNCS(gettimeofday mkdir select popen)

dnl If the system has X, make the X version.
dnl If the system has SVGA, make the SVGA version.
dnl If the system has both, make both.
dnl If the system has neither, exit
PROGS=""
AC_SUBST(PROGS)
if eval "test x$with_x = xyes"; then
  PROGS="$PROGS X_PROGS"
else
  if eval "test x$with_x = x"; then
    AC_CHECK_LIB(X11,XOpenDisplay, PROGS="$PROGS X_PROGS", , )
  fi
fi
if eval "test x$with_svga = xyes"; then
  PROGS="$PROGS SVGA_PROGS"
else
  if eval "test x$with_svga = x"; then
    AC_CHECK_LIB(vgagl, gl_putbox, 
	AC_CHECK_HEADERS(vga.h, PROGS="$PROGS SVGA_PROGS"), , -lvga)
  fi
fi
if test -z "$PROGS"; then
  AC_MSG_ERROR("Found neither SVGA nor X on your system.")
fi

dnl Internationalization
ALL_LINGUAS="ca"
AM_GNU_GETTEXT

dnl GCS: I stole the following from freeciv
dnl Why doesn't AM_GNU_GETTEXT set LIBS appropriately
dnl when -lintl is required??
if test "$gt_cv_func_gettext_libintl" = "yes"; then
    LIBS="-lintl  $LIBS"
fi
dnl DATADIRNAME is generated by AM_GNU_GETTEXT;
dnl note this has to match the path installed by po/Makefile
FC_EXPAND_DIR(LOCALEDIR, '${prefix}/${DATADIRNAME}/locale')
AC_DEFINE_UNQUOTED(LOCALEDIR, "$LOCALEDIR")

AC_OUTPUT(Makefile intl/Makefile po/Makefile.in)
